// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext.kotlinVersion = '1.2.21'

    repositories {
        google()
        jcenter()

        mavenCentral()
        mavenLocal()
    }
    dependencies {
        classpath 'com.google.guava:guava:22.0'
        classpath 'com.android.tools.build:gradle:3.0.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"

        classpath 'com.fernandocejas.frodo:frodo-plugin:0.8.3'
        classpath 'com.github.triplet.gradle:play-publisher:1.1.5'
        classpath 'org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:2.6'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        google()
        jcenter()
        mavenCentral()
    }

    tasks.withType(JavaCompile) {
        options.incremental = true
    }
}

Closure<String> getVersionFromTag = {
    def tag = 'git describe --tags --abbrev=0'.execute()
            .getText()

    return tag.toString().trim()
}

Closure<Integer> getVersionCodeFromNumberOfTags = {
    def tags = 'git tag'.execute()
            .getText()
            .readLines()

    return tags.size()
}

ext {
    projectVersion = getVersionFromTag()
    projectVersionCode = getVersionCodeFromNumberOfTags()

    sourceCompatibilityVersion = JavaVersion.VERSION_1_8
    targetCompatibilityVersion = JavaVersion.VERSION_1_8
}

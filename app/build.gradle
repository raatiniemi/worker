buildscript {
    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath 'com.github.triplet.gradle:play-publisher:1.1.5'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'com.github.triplet.play'
apply plugin: 'jacoco'

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.2'

    defaultConfig {
        applicationId "me.raatiniemi.worker"
        minSdkVersion 21
        targetSdkVersion 28
        versionName projectVersion
        versionCode projectVersionCode

        resConfigs "en"
    }
    if (System.getenv("CI")) {
        signingConfigs {
            release {
                storeFile file(System.getenv("KEYSTORE_PATH"))
                storePassword System.getenv("KEYSTORE_PASSWORD")
                keyAlias System.getenv("KEY_ALIAS")
                keyPassword System.getenv("KEY_PASSWORD")
            }
        }
    }
    buildTypes {
        release {
            if (System.getenv("CI")) {
                signingConfig signingConfigs.release
            }

            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility sourceCompatibilityVersion
        targetCompatibility targetCompatibilityVersion
    }

    sourceSets {
        main {
            res.srcDirs = [
                    'src/main/res-main',
                    'src/main/res-screen/project',
                    'src/main/res-screen/projects',
                    'src/main/res-screen/settings'
            ]
        }
    }

    lintOptions {
        quiet true
        explainIssues true

        abortOnError true
        warningsAsErrors true

        htmlReport false
        xmlReport false
    }

    // AndroidHttpClient was removed from the SDK in v23 of the build tools.
    // This causes issues since robolectric is running against an earlier
    // version of the SDK, and expects the class to be present.
    useLibrary 'org.apache.http.legacy'

    testOptions {
        unitTests.includeAndroidResources = true
        unitTests.returnDefaultValues = true
    }
}

if (System.getenv("PLAY_KEY_PATH")) {
    play {
        jsonFile = file(System.getenv("PLAY_KEY_PATH"))
    }
}

jacoco {
    toolVersion = '0.8.1'
}
tasks.withType(Test) {
    jacoco.includeNoLocationClasses = true
}

dependencies {
    implementation project(':core')
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support:cardview-v7:28.0.0'
    implementation 'com.android.support:design:28.0.0'
    implementation 'com.android.support:recyclerview-v7:28.0.0'
    implementation 'com.android.support:support-annotations:28.0.0'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinVersion"

    implementation 'org.koin:koin-android:1.0.0'

    implementation 'io.reactivex:rxandroid:1.2.0'
    implementation 'com.jakewharton.rxrelay:rxrelay:1.2.0'
    implementation 'com.trello:rxlifecycle-android:1.0'
    implementation 'com.jakewharton.timber:timber:4.7.1'
    implementation 'org.greenrobot:eventbus:3.0.0'
    implementation ('com.h6ah4i.android.widget.advrecyclerview:advrecyclerview:0.11.0') {
        exclude group: 'com.android.support', module: 'appcompat-v7'
        exclude group: 'com.android.support', module: 'recyclerview-v7'
    }

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.6.2'
    testImplementation 'org.robolectric:robolectric:3.5.1'
}

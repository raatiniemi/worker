/*
 * Copyright (C) 2018 Tobias Raatiniemi
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, version 2 of the License.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */

//noinspection GradleDependency
buildscript {
    repositories {
        google()
        maven { url "https://plugins.gradle.org/m2/" }
        jcenter()
    }

    dependencies {
        classpath 'androidx.navigation:navigation-safe-args-gradle-plugin:2.1.0'
        classpath 'com.github.triplet.gradle:play-publisher:2.2.1'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'androidx.navigation.safeargs.kotlin'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'io.fabric'
apply plugin: 'com.github.triplet.play'
apply from: 'jacoco.gradle'

android {
    compileSdkVersion 29
    buildToolsVersion '29.0.2'

    defaultConfig {
        applicationId "me.raatiniemi.worker"
        minSdkVersion 27
        targetSdkVersion 29
        versionName projectVersion
        versionCode projectVersionCode

        if (System.getenv("CI")) {
            testBuildType 'release'
        }
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'

        resConfigs "en"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }
    if (System.getenv("CI")) {
        signingConfigs {
            release {
                storeFile file(System.getenv("KEYSTORE_NAME"))
                storePassword System.getenv("KEYSTORE_PASSWORD")
                keyAlias System.getenv("KEY_ALIAS")
                keyPassword System.getenv("KEY_PASSWORD")
            }
        }
    }
    buildTypes {
        debug {
            manifestPlaceholders = [enableCrashReporting: "false"]
        }
        release {
            manifestPlaceholders = [enableCrashReporting: "true"]

            if (System.getenv("CI")) {
                signingConfig signingConfigs.release
            }

            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            testProguardFile 'proguard-test-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility sourceCompatibilityVersion
        targetCompatibility targetCompatibilityVersion
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    sourceSets {
        test {
            java.srcDirs += 'src/sharedTest/java'
        }
        androidTest {
            java.srcDirs += 'src/sharedTest/java'
            assets.srcDirs += files("$projectDir/schemas".toString())
        }
    }

    lintOptions {
        quiet true
        explainIssues true

        abortOnError true
        warningsAsErrors true

        htmlReport false
        xmlReport false
    }

    testOptions {
        unitTests.includeAndroidResources = true
        unitTests.returnDefaultValues = true
        unitTests.all {
            jacoco {
                includeNoLocationClasses = true
            }
        }
        animationsDisabled = true
    }
}

play {
    track = 'internal'
    enabled = false

    if (System.getenv("GOOGLE_PLAY_ANDROID_DEVELOPER_KEY_NAME")) {
        enabled = true
        serviceAccountCredentials = file(System.getenv("GOOGLE_PLAY_ANDROID_DEVELOPER_KEY_NAME"))
    }
}

dependencies {
    def roomVersion = '2.2.2'
    def lifecycleVersion = '2.1.0'
    def fragmentVersion = '1.1.0'
    def navigationVersion = '2.1.0'
    def coroutineVersion = '1.1.1'
    def koinVersion = '2.0.1'
    def espressoVersion = '3.2.0'
    def archCoreTestingVersion = '2.1.0'

    kapt "androidx.room:room-compiler:$roomVersion"

    implementation project(':core')
    implementation 'androidx.annotation:annotation:1.1.0'
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.preference:preference-ktx:1.1.0'
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycleVersion"
    implementation "androidx.room:room-runtime:$roomVersion"
    implementation 'androidx.paging:paging-runtime:2.1.0'
    implementation "androidx.fragment:fragment:$fragmentVersion"
    implementation "androidx.fragment:fragment-ktx:$fragmentVersion"
    implementation("androidx.fragment:fragment-testing:$fragmentVersion") {
        exclude group: 'androidx.test', module: 'core'
    }
    implementation "androidx.navigation:navigation-fragment-ktx:$navigationVersion"
    implementation "androidx.navigation:navigation-ui-ktx:$navigationVersion"
    implementation 'com.google.android.material:material:1.0.0'

    implementation 'com.google.firebase:firebase-core:17.2.1'
    implementation 'com.crashlytics.sdk.android:crashlytics:2.10.1'

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutineVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutineVersion"

    implementation "org.koin:koin-android:$koinVersion"
    implementation "org.koin:koin-android-viewmodel:$koinVersion"

    implementation 'com.jakewharton.timber:timber:4.7.1'
    implementation 'org.greenrobot:eventbus:3.0.0'

    testImplementation project(':core-test')
    testImplementation 'junit:junit:4.12'
    testImplementation "androidx.arch.core:core-testing:$archCoreTestingVersion"

    androidTestImplementation project(':core-test')
    androidTestImplementation "org.koin:koin-test:$koinVersion"
    androidTestImplementation 'androidx.test:core:1.2.0'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation "androidx.test.espresso:espresso-core:$espressoVersion"
    androidTestImplementation "androidx.test.espresso:espresso-contrib:$espressoVersion"
    androidTestImplementation "androidx.arch.core:core-testing:$archCoreTestingVersion"
    androidTestImplementation "androidx.room:room-testing:$roomVersion"
}

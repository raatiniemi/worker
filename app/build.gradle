apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.fernandocejas.frodo'
apply plugin: 'com.github.triplet.play'
apply plugin: 'jacoco'
apply plugin: 'org.sonarqube'

android {
    compileSdkVersion androidCompileSdkVersion
    buildToolsVersion androidBuildToolsVersion

    defaultConfig {
        applicationId "me.raatiniemi.worker"
        minSdkVersion androidMinSdkVersion
        targetSdkVersion androidTargetSdkVersion
        versionName projectVersion
        versionCode projectVersionCode

        resConfigs "en"

        testInstrumentationRunner 'me.raatiniemi.worker.AndroidTestRunner'
    }
    if (System.getenv("CI")) {
        signingConfigs {
            release {
                storeFile file(System.getenv("KEYSTORE_PATH"))
                storePassword System.getenv("KEYSTORE_PASSWORD")
                keyAlias System.getenv("KEY_ALIAS")
                keyPassword System.getenv("KEY_PASSWORD")
            }
        }
    }
    buildTypes {
        release {
            if (System.getenv("CI")) {
                signingConfig signingConfigs.release
            }

            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility sourceCompatibilityVersion
        targetCompatibility targetCompatibilityVersion
    }

    sourceSets {
        main {
            res.srcDirs = [
                    'src/main/res-main',
                    'src/main/res-screen/project',
                    'src/main/res-screen/projects',
                    'src/main/res-screen/settings'
            ]
        }
    }

    // AndroidHttpClient was removed from the SDK in v23 of the build tools.
    // This causes issues since robolectric is running against an earlier
    // version of the SDK, and expects the class to be present.
    useLibrary 'org.apache.http.legacy'

    testOptions {
        unitTests.includeAndroidResources = true
        unitTests.returnDefaultValues = true

        unitTests.all {
            jacoco {
                includeNoLocationClasses = true
            }
        }
    }
}

if (System.getenv("PLAY_KEY_PATH")) {
    play {
        jsonFile = file(System.getenv("PLAY_KEY_PATH"))
    }
}

Closure<String> combineSourceSetDirectories = { sourceSet ->
    return (sourceSet.java.srcDirs + sourceSet.res.srcDirs).join(',')
}

tasks.sonarqube.dependsOn 'test'

sonarqube {
    properties {
        def binaries = "${buildDir}/intermediates/classes/release"

        def androidPath = android.sdkDirectory.getPath()
        def libraries = "${androidPath}/platforms/${android.compileSdkVersion}/android.jar," +
                "${androidPath}/extras/**/*.jar"

        properties['sonar.projectKey'] = 'worker:android'
        properties['sonar.projectName'] = 'worker'
        properties['sonar.projectVersion'] = projectVersion

        properties['sonar.sourceEncoding'] = 'UTF-8'
        properties['sonar.scm.provider'] = 'git'

        properties['sonar.java.source'] = sourceCompatibilityVersion
        properties['sonar.java.target'] = targetCompatibilityVersion

        properties['sonar.sources'] = combineSourceSetDirectories android.sourceSets.main
        properties['sonar.tests'] = 'src/test/java,src/androidTest/java'

        properties['sonar.java.binaries'] = binaries
        properties['sonar.java.test.binaries'] = binaries

        properties['sonar.java.libraries'] = libraries
        properties['sonar.java.test.libraries'] = libraries

        properties['sonar.junit.reportsPath'] = "${buildDir}/test-results/testReleaseUnitTest"
        properties['sonar.jacoco.reportPaths'] = fileTree(dir: buildDir, includes: ['**/*.exec', '**/*.ec'])
        properties['sonar.java.coveragePlugin'] = 'jacoco'
    }
}

jacoco {
    toolVersion = '0.7.8'
}

dependencies {
    compileOnly deps.jsr250

    kapt deps.daggerCompiler
    kapt deps.butterKnifeCompiler

    debugImplementation deps.leakCanary

    implementation deps.androidSupportAppcompat
    implementation deps.androidSupportCardview
    implementation deps.androidSupportDesign
    implementation deps.androidSupportRecyclerview
    implementation deps.androidSupportAnnotations
    implementation deps.kotlin
    implementation deps.rxandroid
    implementation deps.rxrelay
    implementation deps.rxlifecycle
    implementation deps.timber
    implementation deps.eventbus
    implementation deps.advrecyclerview
    implementation deps.dagger
    implementation deps.butterKnife
    releaseImplementation deps.leakCanaryNoOp

    testImplementation deps.junit
    testImplementation deps.mockitoCore
    testImplementation deps.robolectric
    testImplementation deps.leakCanaryNoOp
    androidTestImplementation(deps.espressoCore) {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.google.code.findbugs', module: 'jsr305'
    }
    androidTestImplementation(deps.androidSupportTestRunner) {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
}

/*
 * Copyright (C) 2018 Tobias Raatiniemi
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, version 2 of the License.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */

//noinspection GradleDependency
buildscript {
    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath 'com.github.triplet.gradle:play-publisher:1.2.2'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.github.triplet.play'

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'

    defaultConfig {
        applicationId "me.raatiniemi.worker"
        minSdkVersion 27
        targetSdkVersion 28
        versionName projectVersion
        versionCode projectVersionCode

        if (System.getenv("CI")) {
            testBuildType 'release'
        }
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'

        resConfigs "en"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }
    if (System.getenv("CI")) {
        signingConfigs {
            release {
                storeFile file(System.getenv("KEYSTORE_PATH"))
                storePassword System.getenv("KEYSTORE_PASSWORD")
                keyAlias System.getenv("KEY_ALIAS")
                keyPassword System.getenv("KEY_PASSWORD")
            }
        }
    }
    buildTypes {
        release {
            if (System.getenv("CI")) {
                signingConfig signingConfigs.release
            }

            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            testProguardFile 'proguard-test-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility sourceCompatibilityVersion
        targetCompatibility targetCompatibilityVersion
    }

    sourceSets {
        main {
            res.srcDirs = [
                    'src/main/res-main',
                    'src/main/res-screen/project',
                    'src/main/res-screen/projects',
                    'src/main/res-screen/settings'
            ]
        }
        androidTest {
            assets.srcDirs += files("$projectDir/schemas".toString())
        }
    }

    lintOptions {
        quiet true
        explainIssues true

        abortOnError true
        warningsAsErrors true

        htmlReport false
        xmlReport false
    }

    // AndroidHttpClient was removed from the SDK in v23 of the build tools.
    // This causes issues since robolectric is running against an earlier
    // version of the SDK, and expects the class to be present.
    useLibrary 'org.apache.http.legacy'

    testOptions {
        unitTests.includeAndroidResources = true
        unitTests.returnDefaultValues = true
    }
}

if (System.getenv("PLAY_KEY_PATH")) {
    play {
        track = 'internal'
        jsonFile = file(System.getenv("PLAY_KEY_PATH"))
    }
}

dependencies {
    kapt 'androidx.room:room-compiler:2.1.0-alpha03'

    implementation project(':core')
    implementation 'androidx.annotation:annotation:1.0.1'
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.0.0'
    implementation 'androidx.room:room-runtime:2.1.0-alpha03'
    implementation 'androidx.paging:paging-runtime:2.0.0'
    implementation 'com.google.android.material:material:1.1.0-alpha02'

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.0.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.0.0'

    implementation 'org.koin:koin-android:1.0.2'
    implementation 'org.koin:koin-android-viewmodel:1.0.2'

    implementation 'io.reactivex:rxandroid:1.2.0'
    implementation 'com.jakewharton.rxrelay:rxrelay:1.2.0'
    implementation 'com.trello:rxlifecycle-android:1.0'
    implementation 'com.jakewharton.timber:timber:4.7.1'
    implementation 'org.greenrobot:eventbus:3.0.0'
    implementation 'com.h6ah4i.android.widget.advrecyclerview:advrecyclerview:1.0.0'

    testImplementation project(':core-test')
    testImplementation 'junit:junit:4.12'
    testImplementation 'androidx.arch.core:core-testing:2.0.0'
    testImplementation 'org.mockito:mockito-core:2.23.4'
    testImplementation 'org.robolectric:robolectric:3.8'

    androidTestImplementation 'androidx.test:core:1.1.0'
    androidTestImplementation 'androidx.test:runner:1.1.1'
    androidTestImplementation 'androidx.test.ext:junit:1.1.0'
    androidTestImplementation 'androidx.room:room-testing:2.1.0-alpha03'
}
